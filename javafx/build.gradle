plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'org.beryx.jlink' version '2.23.3'
}

group 'ru.dimagor555'
version '1.0-SNAPSHOT'

compileJava {
    sourceCompatibility = 14
    targetCompatibility = 14
}

java {
    modularity.inferModulePath = true
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    implementation(project(':domain'))
    implementation(project(':usecase'))
    implementation(project(':presentation'))
    implementation(project(':adapter:repository'))
    implementation(project(':adapter:password-generator'))
    implementation(project(':adapter:id-generator'))
    implementation(project(':adapter:hasher'))
    implementation(project(':adapter:db-dao'))
    implementation(project(':adapter:cryption'))
}

test {
    useJUnitPlatform()
}

javafx {
    version = "14"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

mainClassName = "ru.dimagor555.javafxapp.Main"

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'PasswordManager'
    }

    forceMerge 'istack', 'stax'

    mergedModule {
        requires 'com.fasterxml.classmate';
        requires 'java.logging';
        requires 'jdk.httpserver';
        requires 'java.sql';
        requires 'java.prefs';
        requires 'jdk.jdi';
        requires 'java.rmi';
        requires 'java.xml';
        requires 'com.sun.xml.txw2';
        requires 'java.desktop';
        requires 'jdk.unsupported';
        requires 'java.management';
        requires 'java.naming';
        requires 'jdk.attach';
        requires 'java.scripting';
        requires 'java.datatransfer';
        requires 'java.compiler';
        requires 'java.transaction.xa';
        requires 'com.sun.xml.fastinfoset';
        requires 'java.instrument';
        uses 'org.jboss.logging.LoggerProvider';
        uses 'javax.persistence.spi.PersistenceProvider';
    }
}

jar {
    manifest {
        attributes(
                'Main-Class': 'ru.dimagor555.javafxapp.Main'
        )
    }
}