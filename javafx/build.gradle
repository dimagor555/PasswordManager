plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'org.beryx.jlink' version '2.23.3'
}

group 'ru.dimagor555'
version '1.0-SNAPSHOT'

compileJava {
    sourceCompatibility = 14
    targetCompatibility = 14
}

java {
    modularity.inferModulePath = true
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    implementation(project(':domain'))
    implementation(project(':usecase'))
    implementation(project(':presentation'))
    implementation(project(':adapter:repository'))
    implementation(project(':adapter:password-generator'))
    implementation(project(':adapter:id-generator'))
    implementation(project(':adapter:hasher'))
    implementation(project(':adapter:db-dao'))
    implementation(project(':adapter:cryption'))
    implementation(project(':adapter:clipboard'))
}

test {
    useJUnitPlatform()
}

javafx {
    version = "14"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

mainClassName = "ru.dimagor555.javafxapp.Main"

jlink {
    options = ['--bind-services', '--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    addOptions('--suggest-providers');
    launcher {
        name = 'PasswordManager'
    }

    forceMerge 'jaxb', 'istack', 'stax', 'jakarta'

    mergedModule {
        requires 'com.fasterxml.classmate';
        requires 'java.logging';
        requires 'jdk.httpserver';
        requires 'java.sql';
        requires 'java.prefs';
        requires 'jdk.jdi';
        requires 'java.rmi';
        requires 'java.xml';
        requires 'com.sun.xml.txw2';
        requires 'java.desktop';
        requires 'jdk.unsupported';
        requires 'java.management';
        requires 'java.naming';
        requires 'jdk.attach';
        requires 'java.scripting';
        requires 'java.datatransfer';
        requires 'java.compiler';
        requires 'java.transaction.xa';
        requires 'com.sun.xml.fastinfoset';
        requires 'java.instrument';

        requires 'net.bytebuddy';

        uses 'org.jboss.logging.LoggerProvider';
        uses 'javax.persistence.spi.PersistenceProvider';
        uses 'org.hibernate.integrator.spi.Integrator'
        uses 'org.hibernate.boot.registry.selector.StrategyRegistrationProvider'
        uses 'javax.xml.bind.JAXBContextFactory'
        uses 'org.hibernate.service.spi.ServiceContributor'
        uses 'org.hibernate.boot.spi.MetadataSourcesContributor'
        uses 'org.hibernate.boot.spi.MetadataBuilderInitializer'
        uses 'org.hibernate.boot.spi.MetadataBuilderFactory'
        uses 'org.hibernate.boot.model.TypeContributor'
        uses 'org.hibernate.boot.spi.MetadataContributor'
        uses 'org.hibernate.boot.spi.AdditionalJaxbMappingProducer'
        uses 'org.hibernate.boot.spi.SessionFactoryBuilderFactory'
        uses 'org.hibernate.service.spi.SessionFactoryServiceContributor'
        uses 'org.hibernate.engine.transaction.jta.platform.spi.JtaPlatformProvider'

        provides 'org.apache.commons.logging.LogFactory' with 'org.apache.commons.logging.LogFactoryService';
        provides 'javax.persistence.spi.PersistenceProvider' with 'org.hibernate.jpa.HibernatePersistenceProvider';
        provides 'java.sql.Driver' with 'org.h2.Driver';
    }
}

jar {
    manifest {
        attributes(
                'Main-Class': 'ru.dimagor555.javafxapp.Main'
        )
    }
}